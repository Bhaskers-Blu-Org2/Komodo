procedure update_l2pte(
    {:register OReg(R1)} as_page:int,
    {:register OReg(R2)} mapping:int,
    {:register OReg(R6)} l2pte:int,//page:int,
    {:register OReg(R10)} l1pte:int,
    ghost d:PageDb,
    ghost abs_mapping:Mapping,
    ghost l2pte_abs:L2PTE
)   returns (ghost d':PageDb)
    requires/ensures
        SaneState(this);
    requires 
        validPageDb(d) && wellFormedPageDb(d);
        abs_mapping.l1index == wordToMapping(mapping).l1index;
        abs_mapping.l2index == wordToMapping(mapping).l2index;
        l2pte_abs is SecureMapping ==> (
            pageDbCorrespondsExcluding(this.m,d,l2pte_abs.page)
            && pageDbEntryCorresponds(d[l2pte_abs.page],
                extractPageDbEntry(this.m,l2pte_abs.page)));
        !(l2pte_abs is SecureMapping) ==> pageDbCorresponds(this.m, d);
        isAddrspace(d, as_page);
        validMapping(abs_mapping,d,as_page);
        d[as_page].entry.state is InitState;
        validL2PTE(d, as_page, l2pte_abs);
        l2pte == mkL2Pte(l2pte_abs);
        l1pte == mkL1Pte(d[d[as_page].entry.l1ptnr].entry.l1pt[abs_mapping.l1index], 0);
    ensures 
        validPageDb(d');
        d' == updateL2Pte(d, as_page, abs_mapping, l2pte_abs);
        pageDbCorresponds(this.m,d');
        GlobalsPreservingExcept(old(this),this,set(PageDb()));
{
    reveal_validPageDb();
    //reveal_wordToMapping();
    //reveal_pageDbEntryCorresponds();
    //reveal_pageContentsCorresponds();
    //reveal_pageDbL1PTableCorresponds();
    //reveal_pageDbL2PTableCorresponds();
    ghost var a := as_page;
    ghost var addrspace := d[a].entry;
    assert validAddrspace(d, a);
    ghost var l1 := d[addrspace.l1ptnr].entry;
    ghost var l1pte_abs := fromJust(l1.l1pt[abs_mapping.l1index]);
    ghost var l2pt := d[l1pte_abs].entry.l2pt;
    ghost var l2pt' := l2pt[ abs_mapping.l2index := l2pte_abs ];
    d' := updateL2Pte(d, as_page, abs_mapping, l2pte_abs);
   
    assert l1pte == ARM_L1PTE(page_paddr(l1pte_abs));
    lemma_ARM_L1PTE_Dual(page_paddr(l1pte_abs));
    SUB(r12,l1pte,1);
    assert r12 == page_paddr(l1pte_abs);
    // assert SecurePhysBase() <= page_paddr(l1pte_abs) < SecurePhysBase() + KOM_SECURE_RESERVE();
    // assert SecurePhysBase() <= r12 < SecurePhysBase() + KOM_SECURE_RESERVE();
    paddr_page_impl(r11,r12,r9);
    page_monvaddr_impl(r12,r11,r9);
    assert r12 == page_monvaddr(l1pte_abs);

    LSR(r8,mapping,12);
    AND(r8,r8,0xff);
    forall :: r8 == abs_mapping.l2index {
        reveal_wordToMapping();
        assert r8 == l2indexFromMapping(mapping);
    }
    // Seems like it could be easier to prove this way rather than shifting in 
    // one ins some day
    lemma_LeftShift2(r8);
    LSL(r8,r8,2);
    assert r8 == WordsToBytes(abs_mapping.l2index);
    //assume ValidMem(r12 + r8);
    assert addrInPage(r12 + r8, l1pte_abs);

    assert r6 == mkL2Pte(l2pte_abs);
    STR(r6,r12,r8);

    forall :: pageDbCorrespondsOnly(this.m, d', l1pte_abs)
    {
        reveal_pageDbEntryCorresponds();
        reveal_pageContentsCorresponds();
        ghost var p := l1pte_abs;
        assert pageDbEntryCorresponds(d'[p], extractPageDbEntry(this.m, p));
        forall :: pageDbL2PTableCorresponds(p,
            d'[p].entry, extractPage(this.m, p))
        {
            reveal_pageDbL2PTableCorresponds();
            ghost var base := page_monvaddr(p);
            ghost var mem_page := extractPage(this.m, p);
            forall i :| 0 <= i < NR_L2PTES() && i != abs_mapping.l2index
                :: mem_page[base+WordsToBytes(i)] == mkL2Pte(d'[p].entry.l2pt[i])
            {
                assert base + WordsToBytes(i) !=
                    page_monvaddr(l1pte_abs) + 
                    WordsToBytes(abs_mapping.l2index);
                assert extractPage(this.m, p)[base+WordsToBytes(i)] ==
                    extractPage(old(this.m), p)[base+WordsToBytes(i)];
                assert pageDbCorrespondsOnly(old(this.m), d, p);
            }
            ghost var j := abs_mapping.l2index;
            forall :: mem_page[base+WordsToBytes(j)] == mkL2Pte(d'[p].entry.l2pt[j])
            {
            }
        }
        assert pageContentsCorresponds(p, d'[p], extractPage(this.m, p));
    }

    assert d'[l1pte_abs] is PageDbEntryTyped &&
        d'[l1pte_abs].entry is L2PTable;

    forall :: pageDbCorrespondsExcluding(this.m, d', l1pte_abs) 
    {   
        forall i :| validPageNr(i) && i != l1pte_abs ::
            extractPage(this.m, i) == extractPage(old(this.m), i){}

        forall i :| validPageNr(i) && i != l1pte_abs && d'[i] is PageDbEntryTyped ::
            pageDbEntryCorresponds(d'[i], extractPageDbEntry(this.m, i))
        {
            reveal_pageDbEntryCorresponds();
            assert d[i] == d'[i];
            assert extractPageDbEntry(this.m, i) == 
                extractPageDbEntry(old(this.m), i);
            assert pageDbEntryCorresponds(d[i], extractPageDbEntry(old(this.m), i));
            assert pageDbEntryCorresponds(d'[i], extractPageDbEntry(this.m,i));
        }
        forall i :| validPageNr(i) && i != l1pte_abs && d'[i] is PageDbEntryTyped ::
            pageContentsCorresponds(i, d'[i], extractPage(this.m, i))
        {
            reveal_pageContentsCorresponds();
            assert d[i] == d'[i];
            assert extractPage(this.m, i) == extractPage(old(this.m), i);
            assert pageContentsCorresponds(i, d[i], extractPage(old(this.m),i));
            assert pageContentsCorresponds(i, d'[i], extractPage(this.m,i));
        }
    }
    assert pageDbCorresponds(this.m, d');

    assert d' == updateL2Pte(d,as_page,abs_mapping,l2pte_abs);
    lemma_updateL2PtePreservesPageDb(d,a,abs_mapping,l2pte_abs);
}

procedure is_valid_mapping_target(
    {:register OReg(R1)} addr:int,
    {:register OReg(R2)} mapping:int,
    out {:register OReg(R10)} l1pte:int,
    ghost d:PageDb)
    returns ({:register OReg(R0)} err:int)
    requires/ensures
        SaneState(this);
        validPageDb(d);
        isAddrspace(d,addr);
        pageDbCorresponds(this.m,d);
    ensures
        err == isValidMappingTarget(d, addr, mapping);
        RegPreservingExcept(old(this), this, set(@r0,@r7,@r8,@r9,@r10));
        AllMemInvariant(old(this),this);
        GlobalsInvariant(old(this),this);
        err == KOM_ERR_SUCCESS() ==>(
            validAddrspacePage(d,addr) && 
            l1pte == mkL1Pte(d[d[addr].entry.l1ptnr].entry.l1pt[
                wordToMapping(mapping).l1index], 0) );
{
    ghost var specResult := isValidMappingTarget(d,addr,mapping);
    // if(!addrspace.state.InitState?) then
     //   KOM_ERR_ALREADY_FINAL()
    page_monvaddr_impl(r7,addr,r8);
    LDR(r8,r7,sp_op_const(ADDRSPACE_STATE()));
    forall :: r8 == pageDbAddrspaceStateVal(d[addr].entry.state)
    {
        reveal_pageContentsCorresponds();
        reveal_pageDbAddrspaceCorresponds();
    }
    if ( r8 != sp_op_const(KOM_ADDRSPACE_INIT()) ){
        err := sp_op_const(KOM_ERR_ALREADY_FINAL());
        assert err == specResult;
    } else {
        ghost var abs_mapping := wordToMapping(mapping);
        reveal_wordToMapping();
        LSR(r7,mapping,20); 
        forall :: r7 == abs_mapping.l1index {
            reveal_wordToMapping();
        }
        LSR(r8,mapping,12);
        AND(r8,r8,0xff);
        forall :: r8 == abs_mapping.l2index {
            reveal_wordToMapping();
            assert r8 == l2indexFromMapping(mapping);
        }
        if(r7 >= sp_op_const(KOM_SECURE_NPAGES())){
            err := sp_op_const(KOM_ERR_INVALID_MAPPING());
            assert err == specResult;
        } else if(r8 >= sp_op_const(KOM_SECURE_NPAGES())){
            err := sp_op_const(KOM_ERR_INVALID_MAPPING());
            assert err == specResult;
        } else {
            //if(!perm.r) then KOM_ERR_INVALID_MAPPING()
            AND(r9,mapping,sp_op_const(KOM_MAPPING_R()));
            forall :: r9 == 0 <==> !abs_mapping.perm.r
            {
                reveal_wordToMapping();
                assert r9 != 0 <==> permFromMapping(mapping).r;
            }
            if(r9 == 0){
                err := sp_op_const(KOM_ERR_INVALID_MAPPING());
                assert err == specResult;
            } else {
                if(r7 >= sp_op_const(NR_L1PTES())){
                    err := sp_op_const(KOM_ERR_INVALID_MAPPING());
                    assert err == specResult;
                } else if (r8 >= sp_op_const(NR_L2PTES())) {
                    err := sp_op_const(KOM_ERR_INVALID_MAPPING());
                    assert err == specResult;
                } else {

                    LDRglobaladdr(r10,PageDb());
                    r9 := sp_op_const(PAGEDB_ENTRY_SIZE());
                    /// GGGRRRAAAAAAAAAAArgglebarglebarglebarlge
                    //assert PAGEDB_ENTRY_SIZE() == 8;
                    //assert addr < KOM_SECURE_NPAGES();
                    //assert KOM_SECURE_NPAGES() == 256;
                    //assume isUInt32(PAGEDB_ENTRY_SIZE());
                    //assume isUInt32(KOM_SECURE_NPAGES()); 
                    //assume isUInt32(8 * 256);
                    assert r9 == 8;
                    assert validPageNr(addr);
                    assert isUInt32(addr * r9);
                    MUL(r9,addr,r9);
                    assert r9 == G_PAGEDB_ENTRY(addr) + PAGEDB_ENTRY_TYPE();
                    LDRglobal(r9,PageDb(),r10,r9);
                    forall :: validAddrspacePage(d, addr) {
                        assert isAddrspace(d, addr);
                    }

                    page_monvaddr_impl(r8,addr,r10);

                    LDR(r9,r8,sp_op_const(ADDRSPACE_L1PT())); 

                    ghost var l1p := d[addr].entry.l1ptnr;
                    forall :: validL1PTPage(d,l1p)
                    { 
                        assert validAddrspacePage(d, addr);
                        reveal_validPageDb();
                    
                    }
                    forall :: r9 == page_monvaddr(l1p)
                    {
                        assert pageDbCorrespondsOnly(this.m, d, addr);
                        assert validAddrspacePage(d, addr);
                        reveal_pageContentsCorresponds();
                        assert pageContentsCorresponds(addr, d[addr],
                            extractPage(this.m, addr));
                        reveal_pageDbAddrspaceCorresponds();
                    }
                    
                    assert r7 == abs_mapping.l1index;
                    ghost var l1index := r7;
                    fetch_l1pte(r9,r7,r8,r10,d,l1p);
                    assert r10 == 0 <==> d[l1p].entry.l1pt[l1index] == Nothing;

                    if( r10 == 0 ) { err := sp_op_const(KOM_ERR_INVALID_MAPPING()); }
                    else { err := sp_op_const(KOM_ERR_SUCCESS()); }

                }
            }
        }
    }
}

procedure phys_page_is_insecure_ram_nz({:register OReg(R3)} phys_page:int)
    returns({:register OReg(R12)} ret:int)
    requires/ensures
        SaneState(this);
    requires
        ValidRegOperand(@phys_page);
        ValidRegOperand(@r12);
    ensures 
        r12 == 0 <==> phys_page != 0 && !physPageIsInsecureRam(phys_page);
        RegPreservingExcept(old(this), this, set(@r12)); // TODO FIXME
        AllMemInvariant(old(this),this);
        GlobalsInvariant(old(this),this);
{
    LDRglobaladdr(r12, SecurePhysBaseOp());
    LDRglobal(r12, SecurePhysBaseOp(), r12, 0);
    lemma_RightShift12(r12);
    LSR(r12,r12,12);
    assert r12 == SecurePhysBase() / PAGESIZE();

    if(phys_page >= r12)
    {
        assert !physPageIsInsecureRam(phys_page);
        if(r12 != 0){ r12 := 0; }
        else { r12 := 1; }
    } else {
        r12 := 1;
    }
}

#verbatim
function method L2PTE_CONST_WORD():word
    { (ARM_L2PTE_CONST_BITS() | 0x2) as word }

lemma lemma_extract_kom_mapping_w(mapping:word, res:word)
    requires res == BitwiseXor(BitwiseAnd(mapping,KOM_MAPPING_W()),KOM_MAPPING_W())
    ensures res == KOM_MAPPING_W() || res == 0
    ensures res == 0 <==> wordToMapping(mapping).perm.w
{
    assert WordAsBits(KOM_MAPPING_W()) == 2 by { reveal_WordAsBits(); }
    lemma_WordBitEquiv(KOM_MAPPING_W(), 2);

    var tmp := BitwiseAnd(mapping,KOM_MAPPING_W());
    assert (tmp == KOM_MAPPING_W() || tmp == 0)
        by { reveal_BitAnd(); }
    assert wordToMapping(mapping).perm.w <==> tmp != 0
        by { reveal_wordToMapping(); }

    assert res == if tmp == 0 then KOM_MAPPING_W() else 0
        by { reveal_BitXor(); }
}

lemma lemma_extract_kom_mapping_x(mapping:word, res:word)
    requires res == BitwiseXor(BitwiseAnd(mapping,KOM_MAPPING_X()),KOM_MAPPING_X())
    ensures res == KOM_MAPPING_X() || res == 0
    ensures res == 0 <==> wordToMapping(mapping).perm.x
{
    assert WordAsBits(KOM_MAPPING_X()) == 4 by { reveal_WordAsBits(); }
    lemma_WordBitEquiv(KOM_MAPPING_X(), 4);

    var tmp := BitwiseAnd(mapping,KOM_MAPPING_X());
    assert (tmp == KOM_MAPPING_X() || tmp == 0)
        by { reveal_BitAnd(); }
    assert wordToMapping(mapping).perm.x <==> tmp != 0
        by { reveal_wordToMapping(); }

    assert res == if tmp == 0 then KOM_MAPPING_X() else 0
        by { reveal_BitXor(); }
}

lemma lemma_shift_robit()
    ensures LeftShift(0, 8) == 0;
    ensures LeftShift(KOM_MAPPING_W(), 8) == BitsAsWord(ARM_L2PTE_RO_BIT());
{
    assert WordAsBits(KOM_MAPPING_W()) == 2 by { reveal_WordAsBits(); }
    assert BitsAsWord(ARM_L2PTE_RO_BIT()) == 0x200 by { reveal_BitsAsWord(); }
    reveal_BitShiftLeft();
}

lemma lemma_shift_nxbit()
    ensures RightShift(0, 2) == 0;
    ensures RightShift(KOM_MAPPING_X(), 2) == BitsAsWord(ARM_L2PTE_NX_BIT())
{
    assert WordAsBits(KOM_MAPPING_X()) == 4 by { reveal_WordAsBits(); }
    assert BitsAsWord(ARM_L2PTE_NX_BIT()) == 1 by { reveal_BitsAsWord(); }
    reveal_BitShiftRight();
}

lemma lemma_ARM_L2PTE_impl(pa: word, w: bool, x:bool, ptew: word)
    requires PageAligned(pa)
    requires ptew == BitwiseOr(BitwiseOr(BitwiseOr(pa, L2PTE_CONST_WORD()),
                        if x then 0 else BitsAsWord(ARM_L2PTE_NX_BIT())),
                        if w then 0 else BitsAsWord(ARM_L2PTE_RO_BIT()))
    ensures ptew == ARM_L2PTE(pa, w, x)
{
    var constbits := ARM_L2PTE_CONST_BITS() | 0x2;
    assert L2PTE_CONST_WORD() == BitsAsWord(constbits) by { reveal_BitsAsWord(); }
    lemma_WordBitEquiv(L2PTE_CONST_WORD(), constbits);
    var nxbit := if x then 0 else ARM_L2PTE_NX_BIT();
    var nxword := BitsAsWord(nxbit);
    lemma_WordBitEquiv(nxword, nxbit);
    var robit := if w then 0 else ARM_L2PTE_RO_BIT();
    var roword := BitsAsWord(robit);
    lemma_WordBitEquiv(roword, robit);

    calc {
         ptew;
         BitwiseOr(BitwiseOr(BitwiseOr(pa,L2PTE_CONST_WORD()),nxword),roword);
         { lemma_BitwiseOrAssociative(pa, L2PTE_CONST_WORD(), nxword); }
         BitwiseOr(BitwiseOr(pa,BitwiseOr(L2PTE_CONST_WORD(),nxword)),roword);
         { lemma_BitwiseOrAssociative(pa, BitwiseOr(L2PTE_CONST_WORD(),nxword), roword); }
         BitwiseOr(pa,BitwiseOr(BitwiseOr(L2PTE_CONST_WORD(),nxword),roword));
         { lemma_ExpandBitwiseOr(L2PTE_CONST_WORD(), nxword, roword); }
         BitwiseOr(pa, BitsAsWord(BitOr(BitOr(constbits, nxbit), robit)));
         { lemma_BitsAsWordAsBits(BitOr(BitOr(constbits, nxbit), robit)); }
         BitsAsWord(BitOr(WordAsBits(pa), BitOr(BitOr(constbits, nxbit), robit)));
         ARM_L2PTE(pa, w, x);
     }
}
#endverbatim

procedure ARM_L2PTE_impl(
    {:register OReg(R2)} mapping:int,
    inout {:register OReg(R6)} pte:int, // paddr at input time; pte at return
    out {:register OReg(R4)} tmp:int,
    ghost abs_mapping:Mapping
)
    requires/ensures
        SaneState(this);
    requires
        PageAligned(pte);
        abs_mapping == wordToMapping(mapping);
    ensures
        pte == ARM_L2PTE(old(pte), abs_mapping.perm.w, abs_mapping.perm.x);
        RegPreservingExcept(old(this),this,set(@pte,@tmp));
        AllMemInvariant(old(this),this);
{
    ghost var pa := pte;

    // OR in const bits
    ORR(pte, pte, sp_op_const(L2PTE_CONST_WORD()));
    assert pte == BitwiseOr(pa, L2PTE_CONST_WORD());

    // compute nxbit
    ghost var nxbit := if abs_mapping.perm.x then 0 else BitsAsWord(ARM_L2PTE_NX_BIT());
    AND(tmp,mapping,sp_op_const(KOM_MAPPING_X()));
    EOR(tmp,tmp,sp_op_const(KOM_MAPPING_X()));
    lemma_extract_kom_mapping_x(mapping, tmp);
    assert (tmp == 0 || tmp == KOM_MAPPING_X())
           && (tmp == 0 <==> abs_mapping.perm.x);
    LSR(tmp,tmp,2);
    lemma_shift_nxbit();
    assert (tmp == 0 || tmp == BitsAsWord(ARM_L2PTE_NX_BIT()))
           && (tmp == 0 <==> abs_mapping.perm.x);
    assert tmp == nxbit;
    ORR(pte, pte, tmp);
    assert pte == BitwiseOr(BitwiseOr(pa, L2PTE_CONST_WORD()), nxbit);

    // compute robit
    ghost var robit := if abs_mapping.perm.w then 0 else BitsAsWord(ARM_L2PTE_RO_BIT());
    AND(tmp,mapping,sp_op_const(KOM_MAPPING_W()));
    EOR(tmp,tmp,sp_op_const(KOM_MAPPING_W()));
    lemma_extract_kom_mapping_w(mapping, tmp);
    assert (tmp == 0 || tmp == KOM_MAPPING_W())
           && (tmp == 0 <==> abs_mapping.perm.w);
    LSL(tmp,tmp,8);
    lemma_shift_robit();
    assert (tmp == 0 || tmp == BitsAsWord(ARM_L2PTE_RO_BIT()))
           && (tmp == 0 <==> abs_mapping.perm.w);
    assert tmp == robit;
    ORR(pte, pte, tmp);
    assert pte == BitwiseOr(BitwiseOr(BitwiseOr(pa, L2PTE_CONST_WORD()), nxbit), robit);

    // prove that we did the right thing
    lemma_ARM_L2PTE_impl(pa, abs_mapping.perm.w, abs_mapping.perm.x, pte);
}

procedure mkL2Pte_secure(
    {:register OReg(R2)} mapping:int,
    inout {:register OReg(R6)} page:int,
    ghost abs_mapping:Mapping,
    ghost l2pte:L2PTE
)
    requires/ensures
        SaneState(this);
    requires
        abs_mapping == wordToMapping(mapping);
        l2pte is SecureMapping;
        (l2pte.page == page
            && abs_mapping.perm.x == l2pte.exec
            && abs_mapping.perm.w == l2pte.write);
    ensures
        page == mkL2Pte(l2pte);
        RegPreservingExcept(old(this),this,set(@r4,@r6));
        AllMemInvariant(old(this),this);
{
    page_paddr_impl(r6,r6,r4);
    ARM_L2PTE_impl(r2, r6, r4, abs_mapping);
}

procedure kom_smc_map_secure(
    {:register OReg(R0)} page:int,
    {:register OReg(R1)} as_page:int,
    {:register OReg(R2)} mapping:int,
    {:register OReg(R3)} phys_page:int,
    out {:register OReg(R0)} err:int,
    ghost s:SysState)
    returns (ghost s':SysState)
    requires
        this == s.hw;
        SaneState(this);
        validSysState'(s);
    ensures
        this == s'.hw;
        SaneState(this);
        tuple(s'.d,err) == smc_mapSecure_premium(s.d, page, as_page,
            mapping, phys_page);
        // TODO
        // For now, avoid proving that we haven't trampled the current 
        // dispatcher page. We probably need to either: 1) add errors for the case 
        // that the current dispatcher page is given in R0, or 2) allow the 
        // current dispatcher to either be a valid dispatcher or free and only 
        // explicitly require it to be non-free on a resume. In the second 
        // case, we need to probably add an error for the case where its free
        almostValidSysState(s');
        pageDbCorresponds(s'.hw.m, s'.d);
{
    ghost var specResult := smc_mapSecure_premium(s.d, page, as_page,
            mapping, phys_page);
    //isValidMappingTarget
    //physPageIsInsecureRam
    //allocatePage
    //SecureMapping

    r6 := page;
    //if(!isAddrspace(pageDbIn, addrspacePage)) then
    //    (pageDbIn, KOM_ERR_INVALID_ADDRSPACE())
    if (as_page >= sp_op_const(KOM_SECURE_NPAGES())) {
        err := sp_op_const(KOM_ERR_INVALID_ADDRSPACE());
        s' := s.(hw:=this);
    } else {
        LDRglobaladdr(r12, PageDb());
        assert sp_eval_op_addr(this, @r12) == AddressOfGlobal(PageDb());
        assert validPageNr(as_page);
        err := sp_op_const(PAGEDB_ENTRY_SIZE());
        MUL(r4,as_page,r0);
        assert r4 == G_PAGEDB_ENTRY(as_page) + PAGEDB_ENTRY_TYPE();
        LDRglobal(r0, PageDb(), r12, r4);

        forall :: r0 == pageDbEntryTypeVal(s.d[as_page]) {
            reveal_pageDbEntryCorresponds();
        }

        if (r0 != sp_op_const(KOM_PAGE_ADDRSPACE())) {
            assert !isAddrspace(s.d, as_page);
            err := sp_op_const(KOM_ERR_INVALID_ADDRSPACE());
            assert !isAddrspace(s.d, as_page) ==> specErr(specResult) == 
                KOM_ERR_INVALID_ADDRSPACE();
            assert err == specErr(specResult);
            s' := s.(hw:=this);
            forall :: validSysState'(s')
                    { reveal_validPageDb(); }
        } else {
            r5 := as_page;
            err:= is_valid_mapping_target(as_page,mapping,r9,s.d);
            if( err != sp_op_const(KOM_ERR_SUCCESS()) ) {
            assert err == specErr(specResult);
                s' := s.(hw:=this);
                forall :: validSysState'(s')
                    { reveal_validPageDb(); }
            } else {

                r1 := r5;
                assert as_page == old(as_page);
                assert r1 == old(as_page);
                r12 := phys_page_is_insecure_ram_nz(phys_page);
                if(r12 == 0) {
                    err := sp_op_const(KOM_ERR_INVALID_PAGENO());
                    assert err == specErr(specResult);
                    s' := s.(hw:=this);
                    forall :: validSysState'(s')
                        { reveal_validPageDb(); }
                } else {
                    ghost var d';
                    ghost var entry := PageDbEntryTyped(as_page, DataPage); 
                    assert entry.addrspace == as_page;
                    assert validAddrspacePage(s.d, entry.addrspace);
                    assert r1 == old(as_page);
                    LDRglobaladdr(r12, PageDb());
                    assert r6 == old(page);
                    d' := allocate_page(r6,as_page,
                        sp_op_const(KOM_PAGE_DATA()),
                        r12,r11,r9,err,s.d,entry);
                    assert err == KOM_ERR_SUCCESS() ==>
                        (pageDbCorrespondsExcluding(this.m, d', r6)
                        && pageDbEntryCorresponds(d'[r6],extractPageDbEntry(this.m, r6)));
                    if( err != sp_op_const(KOM_ERR_SUCCESS()) ){
                        s' := s.(d:=d').(hw:=this);
                        assert err == specErr(specResult);
                        assert s'.d == specPageDb(specResult);
                        assert pageDbCorresponds(s'.hw.m,s'.d);
                        assert validSysState'(s');
                    } else {
                        assert r6 == old(page);
                        assert as_page == old(as_page);
                        assert validPageNr(r6);
                        assert err == KOM_ERR_SUCCESS();
                        assert pageDbCorrespondsExcluding(this.m,d',r6);
                        assert pageDbEntryCorresponds(d'[r6],
                                extractPageDbEntry(this.m,r6));
                        ghost var abs_mapping := wordToMapping(mapping);
                        assert isValidMappingTarget(s.d, as_page, mapping)
                            == KOM_ERR_SUCCESS();
                        forall :: isValidMappingTarget(d', as_page, mapping)
                            == KOM_ERR_SUCCESS()
                        {
                            lemma_allocatePage_preservesMappingGoodness(
                                s.d,r6,as_page,entry.entry,
                                d',err,mapping);
                        }
                        reveal_wordToMapping();
                        ghost var l2pte := SecureMapping(r6, 
                            abs_mapping.perm.w, abs_mapping.perm.x);
                       
                        assert validL2PTE(d', as_page, l2pte);
                        forall :: wellFormedPageDb(d') { reveal_validPageDb(); }
                        ghost var d'';
                        assert validPageNr(r6); 
                        mkL2Pte_secure(mapping,r6,abs_mapping,l2pte);
                        assert r10 == mkL1Pte(s.d[s.d[as_page].entry.l1ptnr].entry.l1pt[abs_mapping.l1index], 0);
                        assert r10 == mkL1Pte(d'[d'[as_page].entry.l1ptnr].entry.l1pt[abs_mapping.l1index], 0);
                        d'' := update_l2pte(as_page,mapping,r6,r10,d',
                            abs_mapping,l2pte);
                        assert pageDbCorresponds(this.m, d'');
                        assert d'' == updateL2Pte(d', as_page, abs_mapping, l2pte);
                        err := sp_op_const(KOM_ERR_SUCCESS());
                        s' := s.(d:=d'').(hw:=this);
                        assert pageDbCorresponds(s'.hw.m,s'.d);
                        assert err == specErr(specResult);
                        assert s'.d == specPageDb(specResult);
                        assert almostValidSysState(s');
                    }
                }
            }
        }
    }
}

