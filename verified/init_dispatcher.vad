include {:verbatim} "kom_common.i.dfy"
include {:verbatim} "pagedb.i.dfy"
include {:verbatim} "smcapi.i.dfy"

include "valedecls.vad"
include "kom_utils.vad"
include "allocate_page.vad"
include "map_utils.vad"
include "memset.vad"

procedure init_dispatcher_page_zero(operand ptr:reg, operand tmp:reg)
    requires/ensures
        SaneState(this);
    modifies
        mem;
    requires
        ValidMemWords(ptr, 8);
    ensures
        forall i :: ValidMemForRead(i) && !(old(ptr) <= i <= WordOffset(old(ptr), 7)) ==>
            MemContents(this.m, i) == MemContents(old(this).m, i);
        AddrMemContents(mem, WordOffset(old(ptr), 0)) == tmp;
        AddrMemContents(mem, WordOffset(old(ptr), 1)) == tmp;
        AddrMemContents(mem, WordOffset(old(ptr), 2)) == tmp;
        AddrMemContents(mem, WordOffset(old(ptr), 3)) == tmp;
        AddrMemContents(mem, WordOffset(old(ptr), 4)) == tmp;
        AddrMemContents(mem, WordOffset(old(ptr), 5)) == tmp;
        AddrMemContents(mem, WordOffset(old(ptr), 6)) == tmp;
        AddrMemContents(mem, WordOffset(old(ptr), 7)) == tmp;
{
    STR(tmp, ptr, const(0 * WORDSIZE));
    STR(tmp, ptr, const(1 * WORDSIZE));
    STR(tmp, ptr, const(2 * WORDSIZE));
    STR(tmp, ptr, const(3 * WORDSIZE));
    STR(tmp, ptr, const(4 * WORDSIZE));
    STR(tmp, ptr, const(5 * WORDSIZE));
    STR(tmp, ptr, const(6 * WORDSIZE));
    STR(tmp, ptr, const(7 * WORDSIZE));
}

procedure{:timeLimitMultiplier 2} init_dispatcher_page_zeros(inout operand ptr:reg, operand tmp:reg)
    requires/ensures
        SaneState(this);
    modifies
        mem;
    requires
        ValidMemWords(ptr + DISP_CTXT_USER_WORDS, 8);
        ValidMemWords(ptr + DISP_CTXT_VERIFY_MEASUREMENT, 8);
        @ptr != @tmp;
        @ptr != OSP;
        @tmp != OSP;
    ensures
        forall i :: ValidMemForRead(i)
            && !(old(ptr) + DISP_CTXT_USER_WORDS <= i <= WordOffset(old(ptr) + DISP_CTXT_USER_WORDS, 7))
            && !(old(ptr) + DISP_CTXT_VERIFY_MEASUREMENT <= i <= WordOffset(old(ptr) + DISP_CTXT_VERIFY_MEASUREMENT, 7))
            ==>
            MemContents(this.m, i) == MemContents(old(this).m, i);
        AddrMemContents(mem, WordOffset(old(ptr) + DISP_CTXT_USER_WORDS, 0)) == tmp;
        AddrMemContents(mem, WordOffset(old(ptr) + DISP_CTXT_USER_WORDS, 1)) == tmp;
        AddrMemContents(mem, WordOffset(old(ptr) + DISP_CTXT_USER_WORDS, 2)) == tmp;
        AddrMemContents(mem, WordOffset(old(ptr) + DISP_CTXT_USER_WORDS, 3)) == tmp;
        AddrMemContents(mem, WordOffset(old(ptr) + DISP_CTXT_USER_WORDS, 4)) == tmp;
        AddrMemContents(mem, WordOffset(old(ptr) + DISP_CTXT_USER_WORDS, 5)) == tmp;
        AddrMemContents(mem, WordOffset(old(ptr) + DISP_CTXT_USER_WORDS, 6)) == tmp;
        AddrMemContents(mem, WordOffset(old(ptr) + DISP_CTXT_USER_WORDS, 7)) == tmp;
        AddrMemContents(mem, WordOffset(old(ptr) + DISP_CTXT_VERIFY_MEASUREMENT, 0)) == tmp;
        AddrMemContents(mem, WordOffset(old(ptr) + DISP_CTXT_VERIFY_MEASUREMENT, 1)) == tmp;
        AddrMemContents(mem, WordOffset(old(ptr) + DISP_CTXT_VERIFY_MEASUREMENT, 2)) == tmp;
        AddrMemContents(mem, WordOffset(old(ptr) + DISP_CTXT_VERIFY_MEASUREMENT, 3)) == tmp;
        AddrMemContents(mem, WordOffset(old(ptr) + DISP_CTXT_VERIFY_MEASUREMENT, 4)) == tmp;
        AddrMemContents(mem, WordOffset(old(ptr) + DISP_CTXT_VERIFY_MEASUREMENT, 5)) == tmp;
        AddrMemContents(mem, WordOffset(old(ptr) + DISP_CTXT_VERIFY_MEASUREMENT, 6)) == tmp;
        AddrMemContents(mem, WordOffset(old(ptr) + DISP_CTXT_VERIFY_MEASUREMENT, 7)) == tmp;
{
    ADD(ptr, ptr, const(DISP_CTXT_USER_WORDS));
    init_dispatcher_page_zero(ptr, tmp);
    SUB(ptr, ptr, const(DISP_CTXT_USER_WORDS));
    ADD(ptr, ptr, const(DISP_CTXT_VERIFY_MEASUREMENT));
    init_dispatcher_page_zero(ptr, tmp);
}

procedure{:timeLimitMultiplier 3} init_dispatcher_page(
    inout operand disp_page:reg,
    operand entrypoint:reg,
    out operand tmp:reg)
    requires/ensures
        SaneState(this);
    requires
        @disp_page != @entrypoint && @disp_page != @tmp && @disp_page != OSP;
        @entrypoint != @tmp && @entrypoint != OSP && @tmp != OSP;
        validPageNr(disp_page);
    reads
        globals;
    modifies
        mem;
    ensures
        MemPreservingExcept(old(this), this, page_monvaddr(old(disp_page)),
                            page_monvaddr(old(disp_page)) + PAGESIZE);
        SmcProcedureInvariant(old(this), this);
        let vzeros := seq(0, 0, 0, 0, 0, 0, 0, 0);
        let e := Dispatcher(old(entrypoint), false, initDispCtxt(), vzeros, vzeros);
        wellFormedPageDbEntryTyped(e);
        pageDbDispatcherCorresponds(old(disp_page), e, extractPage(this.m, old(disp_page)));
{
    ghost var dispva := page_monvaddr(old(disp_page));
    assert dispva >= KOM_DIRECTMAP_VBASE + MonitorPhysBase();
    page_monvaddr_impl(disp_page, disp_page, tmp);
    assert disp_page == dispva;

    assert entrypoint == old(entrypoint);
    STR(entrypoint, disp_page, const(DISPATCHER_ENTRYPOINT));

    MOV(tmp, 0);
    STR(tmp, disp_page, const(DISPATCHER_ENTERED));
    init_dispatcher_page_zeros(disp_page, tmp);

    assert MemPreservingExcept(old(this), this, dispva, dispva + DISP_SIZE);

    ghost var vzeros := seq(0, 0, 0, 0, 0, 0, 0, 0);
    ghost var e := Dispatcher(old(entrypoint), false, initDispCtxt(), vzeros, vzeros);
    ghost var pg := extractPage(this.m, old(disp_page));
    assert pageDbDispatcherCorresponds(old(disp_page), e, pg)
    by {
        reveal pageDbDispatcherCorresponds;
        reveal pageDbDispatcherContextCorresponds;
        reveal pageDbDispatcherVerifyStateCorresponds;
        assert pg[dispva + DISPATCHER_ENTRYPOINT] == old(entrypoint);
        assert pg[dispva + DISPATCHER_ENTERED] == to_i(false);
    }
}

procedure {:frame false} kom_smc_init_dispatcher(
    inout operand disp_page:reg, // r1
    operand as_page:reg, // r2
    inout operand entrypoint:reg, // r3
    operand pagedb_base:addr,
    out operand err:reg,
    out operand tmp:reg,
    out operand tmp2:reg,
    ghost pagedb_in: PageDb) returns (ghost pagedb: PageDb)
    requires/ensures
        SaneState(this);
    requires
        ValidRegOperand(@disp_page) && ValidRegOperand(@as_page)
            && ValidRegOperand(@entrypoint) && ValidRegOperand(@pagedb_base)
            && ValidRegOperand(@err) && ValidRegOperand(@tmp) && ValidRegOperand(@tmp2);
        @disp_page == OReg(R1) && @as_page == OReg(R2) && @entrypoint == OReg(R3) && @err == OReg(R0);
        @pagedb_base == OReg(R12) && pagedb_base == AddressOfGlobal(PageDb());
        @disp_page != @as_page && @disp_page != @entrypoint && @disp_page != @err
            && @disp_page != @tmp && @disp_page != @tmp2 && @disp_page != @pagedb_base
            && @disp_page != OSP;
        @err != @as_page && @err != @entrypoint && @err != @tmp && @err != @tmp2
            && @err != @pagedb_base && @err != OSP;
        @tmp != @as_page && @tmp != @entrypoint && @tmp != @tmp2 && @tmp != @pagedb_base && @tmp != OSP;
        @tmp2 != @as_page && @tmp2 != @entrypoint && @tmp2 != @pagedb_base && @tmp2 != OSP;
        StackBytesRemaining(this, MEASURE_STACK_BYTES + STACKSIZE_SHA_UPDATE * WORDSIZE);
        validPageDb(pagedb_in);
        pageDbCorresponds(this.m, pagedb_in);
    modifies
        globals; mem;
    ensures
        SmcProcedureInvariant(old(this),this);
        tuple(pagedb, err)
            == old(smc_initDispatcher(pagedb_in, disp_page, as_page, entrypoint));
        validPageDb(pagedb) && pageDbCorresponds(this.m, pagedb);
{
    ghost var specResult := smc_initDispatcher(pagedb_in, disp_page, as_page, entrypoint);
    initDispatcherPreservesPageDBValidity(pagedb_in, disp_page, as_page, entrypoint);
    pagedb := pagedb_in;

    if (as_page >= const(KOM_SECURE_NPAGES)) {
        MOV(err, const(KOM_ERR_INVALID_ADDRSPACE));
    } else {
        assert validPageNr(as_page);
        load_page_type(as_page, pagedb_base, tmp, tmp, pagedb);

        if (tmp != const(KOM_PAGE_ADDRSPACE)) {
            MOV(err, const(KOM_ERR_INVALID_ADDRSPACE));
        } else {
            assert isAddrspace(pagedb, as_page);
            ghost var vzeros := seq(0, 0, 0, 0, 0, 0, 0, 0);
            ghost var e := Dispatcher(entrypoint, false, initDispCtxt(), vzeros, vzeros);
            ghost var entry := PageDbEntryTyped(as_page, e);
            pagedb := allocate_page(disp_page, as_page,
                                    const(KOM_PAGE_DISPATCHER),
                                    pagedb_base, tmp, tmp2, err, pagedb, entry, false);

            if (err == const(KOM_ERR_SUCCESS)) {
                assert err == specErr(specResult);
                assert pagedb == specPageDb(allocatePage(pagedb_in, disp_page, as_page, e));
                allocatePagePreservesPageDBValidity(pagedb_in, disp_page, as_page, e);

                assert pageDbCorrespondsExcluding(this.m, pagedb, disp_page);
                assert pageDbEntryCorresponds(pagedb[disp_page],
                                         extractPageDbEntry(this.m, disp_page));
                ghost var tmpstate := this;

                // update dispatcher page
                init_dispatcher_page(disp_page, entrypoint, tmp);

                assert pageDbCorrespondsOnly(this.m, pagedb, old(disp_page))
                    by { reveal pageContentsCorresponds; }
                AllButOnePagePreserving(old(disp_page), tmpstate, this);
                assert pageDbCorresponds(this.m, pagedb);

                MOV(r1, as_page);
                MOV(r2, const(KOM_SMC_INIT_DISPATCHER));
                assert entrypoint == old(entrypoint);
                pagedb := kom_smc_map_measure(false, r1, r2, entrypoint, seq(), 0, 0, pagedb);
                assert pagedb == specPageDb(specResult);
                MOV(err, const(KOM_ERR_SUCCESS));
            } else {
                assert pagedb == pagedb_in;
                assert pageDbCorresponds(this.m, pagedb);
            }
        }
    }
}
