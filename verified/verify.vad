include "attest.vad"
include "verify_input.vad"

procedure {:timeLimitMultiplier 3} svc_returning_verify_inner(
    ghost exstate: state,
    ghost stack_bytes: int,
    ghost pagedb: PageDb,
    ghost dispPg: PageNr)
    returns (
    ghost mac_ptr:word,
    ghost mac:seq(word)
    )

    reads
        globals;
    modifies
         r0; r1; r2; r3; r4; r5; r6; r7; r8; r9; r10; r11; r12; sp; lr;
         mem;
    requires/ensures
        SaneState(this);
        StackBytesRemaining(this, stack_bytes);
    requires
        ValidState(exstate) && mode_of_state(exstate) != User;
        stack_bytes >= STACKSIZE_ATTEST * WORDSIZE;
        isReturningSvc(exstate);
        CoreRegPreservingExcept(exstate, this, set(OSP, OLR));
        validPageDb(pagedb);
        pageDbCorresponds(this.m, pagedb);
        validDispatcherPage(pagedb, dispPg);
        finalDispatcher(pagedb, dispPg);
        GlobalFullContents(this.m, CurDispatcherOp()) == seq(page_monvaddr(dispPg));
//        r10 == page_monvaddr(dispPg);
//        r11 == page_monvaddr(pagedb[dispPg].addrspace);
        ValidMemRange(sp - STACKSIZE_ATTEST * WORDSIZE, sp);
    ensures
        // Framing
        StackPreserving(old(this), this);
        NonStackMemPreserving(old(this), this);
        pageDbCorresponds(this.m, pagedb);
        sp == old(sp);

        // Results
        mac_ptr == sp - (STACKSIZE_ATTEST - STACKSIZE_HMAC) * WORDSIZE;
        ValidMemRange(mac_ptr, mac_ptr + SHA_CTXSIZE * WORDSIZE);
        mac == AddrMemContentsSeq(mem, mac_ptr, SHA_CTXSIZE);
        mac == svcHmacVerify(exstate, pagedb, dispPg);
{
    ghost var addrspace := pagedb[dispPg].addrspace;
    assert validAddrspacePage(pagedb, addrspace) by { reveal validPageDb; }

    // Stack layout [size in words]:
    //   sp := mac_output[8] || message [8+16] || gap[1] || tmp_ptr[2*SHA_BLOCKSIZE] || key_ptr[HMAC_KEYSIZE_WORDS]
    // where 
    //   message = user_words[8] || enclave_measurement [8] || 0 [8]
    // Total stack usage == 8 + 24 + 1 + 2 * SHA_BLOCKSIZE + HMAC_KEYSIZE_WORDS
    // Plus STACKSIZE_HMAC
    SUB(sp, sp, const((STACKSIZE_ATTEST - STACKSIZE_HMAC) * WORDSIZE));
    assert StackBytesRemaining(this, STACKSIZE_HMAC * WORDSIZE);
    
    // Arrange the user words in memory after the gap for the MAC output
    ghost var pre_input_sp;
    pre_input_sp := sp;
    arrange_verification_input(pagedb, dispPg);

    assert pageDbCorresponds(this.m, pagedb) by {
        lemma_SameMemAndGlobalsPreservesPageDb(old(this), this, pagedb);
    }

    ghost var input;
    input := pagedb[dispPg].entry.verify_words + pagedb[dispPg].entry.verify_measurement + SeqRepeat(8, 0);

//    ghost var spec_hmac;
//    spec_hmac := HMAC_SHA256(AttestKey(), WordSeqToBytes(input));


    calc {
        old(sp) - (sp + SHA_CTXSIZE * WORDSIZE);
        old(sp) - (old(sp) - (STACKSIZE_ATTEST - STACKSIZE_HMAC) * WORDSIZE  + SHA_CTXSIZE * WORDSIZE);
        (STACKSIZE_ATTEST - STACKSIZE_HMAC) * WORDSIZE - SHA_CTXSIZE * WORDSIZE;
        (STACKSIZE_ATTEST - STACKSIZE_HMAC - SHA_CTXSIZE) * WORDSIZE;
    }

    // Prove stack memory is valid
    ghost var STACKSIZE_LOCAL := STACKSIZE_ATTEST - STACKSIZE_HMAC;
    ghost var size := STACKSIZE_ATTEST - STACKSIZE_HMAC - SHA_CTXSIZE;
    calc {
        true;
        ValidMemRange(old(sp) - STACKSIZE_ATTEST * WORDSIZE, old(sp));
            { lemma_ValidMemRange_offset(old(sp) - STACKSIZE_ATTEST * WORDSIZE, STACKSIZE_ATTEST, STACKSIZE_ATTEST - STACKSIZE_HMAC); }
        ValidMemRange(old(sp) - (STACKSIZE_ATTEST - STACKSIZE_HMAC) * WORDSIZE, old(sp));
        ValidMemRange(sp, old(sp));
            { lemma_ValidMemRange_reduced(sp, STACKSIZE_LOCAL, STACKSIZE_LOCAL - SHA_CTXSIZE); }
        ValidMemRange(sp + SHA_CTXSIZE * WORDSIZE, old(sp));
            { lemma_ValidMemRange_reduced(sp + SHA_CTXSIZE * WORDSIZE, size, size - 4 * SHA_CTXSIZE); }
        ValidMemRange(sp + SHA_CTXSIZE * WORDSIZE, sp + 4 * SHA_CTXSIZE * WORDSIZE);
    }

    mac := compute_hmac(STACKSIZE_HMAC * WORDSIZE, pagedb, dispPg, input, old(sp));
//    assert spec_hmac == mac;
    assert mac == svcHmacVerify(exstate, pagedb, dispPg);
    mac_ptr := sp;

    calc {
        sp + (STACKSIZE_ATTEST - STACKSIZE_HMAC) * WORDSIZE;
        sp + STACKSIZE_LOCAL * WORDSIZE;
        (old(sp) - STACKSIZE_LOCAL * WORDSIZE) + STACKSIZE_LOCAL * WORDSIZE;  
        old(sp);
    }
    assert isUInt32(old(sp));
    ADD(sp, sp, const((STACKSIZE_ATTEST - STACKSIZE_HMAC) * WORDSIZE));
    assert sp == old(sp);

    assert pageDbCorresponds(this.m, pagedb) by {
        lemma_SameMemAndGlobalsPreservesPageDb(old(this), this, pagedb);
    }
}

#verbatim

predicate {:opaque} comparison_invariant(r1:word, index:nat, regs:seq<word>, contents:seq<word>)
    requires 0 <= index <= |regs|;
    requires |regs| == |contents|;
{
    r1 <= index
 && (r1 == index <==> regs[..index] == contents[..index])
}

lemma lemma_comparison_invariant_implies(r1:word, regs:seq<word>, contents:seq<word>)
    requires |regs| == |contents|;
    requires comparison_invariant(r1, |regs|, regs, contents);
    ensures  r1 == |regs| <==> regs == contents;
{
    reveal comparison_invariant();
    assert SeqDrop(regs, |regs|) == regs;
    assert SeqDrop(contents, |regs|) == contents;
}

lemma lemma_extend_comparison_invariant(old_r1:word, r1:word, index:nat, regs:seq<word>, contents:seq<word>)
    requires |regs| == |contents|;
    requires 0 <= index < |regs|;
    requires comparison_invariant(old_r1, index, regs, contents);
    requires old_r1 == r1 ==> regs[index] != contents[index];
    requires old_r1 != r1 ==> r1 == old_r1 + 1 && regs[index] == contents[index];
    ensures  comparison_invariant(r1, index + 1, regs, contents);
{
    reveal comparison_invariant();
//    
//    if old_r1 == index {
//        if old_r1 == r1 {
//            assert comparison_invariant(r1, index + 1, regs, contents);
//        } else {
//            assert comparison_invariant(r1, index + 1, regs, contents);
//        }
//    } else {
//        if old_r1 == r1 {
//            assert regs[..index] != contents[..index];
//            assert regs[..index + 1] == regs[..index] + [regs[index]];
//            assert contents[..index + 1] == contents[..index] + [contents[index]];
//            assert r1 != index + 1;
//            assert contents[..index + 1] != regs[..index + 1];
//            assert comparison_invariant(r1, index + 1, regs, contents);
//        } else {
//            assert comparison_invariant(r1, index + 1, regs, contents);
//        }
//    }
}

type uint16 = x:int | 0 <= x < 0x1_0000_0000

#endverbatim

procedure compare_mem_to_reg(
        operand reg:word, 
        inline index:byte,
        ghost regs:seq(word),
        ghost contents:seq(word))
    reads
        mem; sp;
    modifies 
        r0; r1;
    requires/ensures
        SaneState(this);
    requires
        @reg != OReg(R0) && @reg != OReg(R1);
        ValidMemRange(sp, sp + SHA_CTXSIZE * WORDSIZE);
        0 <= index < 8;
        0 <= r1 < 8;
        SeqLength(regs) == 8;
        SeqLength(contents) == 8;
        regs[index] == reg;
        contents[index] == AddrMemContents(mem, sp + index * WORDSIZE);
        comparison_invariant(r1, index, regs, contents);
    ensures
        comparison_invariant(r1, index + 1, regs, contents);
        r1 == old(r1) || r1 == old(r1) + 1;
{
    lemma_WordAlignedAdd(sp, WordsToBytes(index));

    LDR(r0, sp, const(WordsToBytes(index)));
    assert r0 == contents[index];

    if (r0 == reg) {
        ADD(r1, r1, 1);
    }
    lemma_extend_comparison_invariant(old(r1), r1, index, regs, contents);
}

procedure {:timeLimitMultiplier 2} compare_memory_to_regs(ghost contents:seq(word))
    returns (ghost regs:seq(word))
    reads
        mem; r1; r2; r3; r4; r5; r6; r7; r8; sp;
    modifies 
        r0; r1;
    requires/ensures
        SaneState(this);
    requires
        ValidMemRange(sp, sp + SHA_CTXSIZE * WORDSIZE);
        SeqLength(contents) == 8;
        AddrMemContentsSeq(mem, sp, SHA_CTXSIZE) == contents;
    ensures
        r1 == (if (regs == contents) then 1 else 0);
        regs == old(seq(r1, r2, r3, r4, r5, r6, r7, r8));
{
    regs := seq(r1, r2, r3, r4, r5, r6, r7, r8);

    assert WordAligned(sp);
    LDR(r0, sp, 0);
    assert r0 == contents[0];
    if (r0 == r1) {
        MOV(r1, 1); 
    } else {
        MOV(r1, 0);
    }
    assert comparison_invariant(r1, 1, regs, contents) by { reveal comparison_invariant; }

    compare_mem_to_reg(r2, 1, regs, contents);
    compare_mem_to_reg(r3, 2, regs, contents);
    compare_mem_to_reg(r4, 3, regs, contents);
    compare_mem_to_reg(r5, 4, regs, contents);
    compare_mem_to_reg(r6, 5, regs, contents);
    compare_mem_to_reg(r7, 6, regs, contents);
    compare_mem_to_reg(r8, 7, regs, contents);

    lemma_comparison_invariant_implies(r1, regs, contents);
    assert {:split_here} true;
    if (r1 == 8) {
        assert regs == contents;
        MOV(r1, 1);
    } else {
        assert regs != contents;
        MOV(r1, 0);
    }
    //assert {:split_here} true;
}

procedure {:frame false} {:timeLimitMultiplier 2} svc_returning_verify_inner'(
    ghost exstate: state,
    ghost stack_bytes: int,
    ghost pagedb: PageDb,
    ghost dispPg: PageNr
    ) returns (
    ghost regs:seq(word),
    ghost mac:seq(word)
    )
    reads
        globals;
    modifies
         r0; r1; r2; r3; r4; r5; r6; r7; r8; r9; r10; r11; r12; sp; lr;
         mem;
    requires/ensures
        SaneState(this);
        StackBytesRemaining(this, stack_bytes);
    requires
        ValidState(exstate) && mode_of_state(exstate) != User;
        stack_bytes >= WordsToBytes(STACKSIZE_ATTEST + 14);  // +14 to store reigsters
        isReturningSvc(exstate);
        CoreRegPreservingExcept(exstate, this, set(OSP, OLR));
        validPageDb(pagedb);
        pageDbCorresponds(this.m, pagedb);
        validDispatcherPage(pagedb, dispPg);
        finalDispatcher(pagedb, dispPg);
        GlobalFullContents(this.m, CurDispatcherOp()) == seq(page_monvaddr(dispPg));
        r0 == KOM_SVC_VERIFY_STEP2;
        ValidMemRange(sp - WordsToBytes(STACKSIZE_ATTEST + 14), sp);
    ensures
        pageDbCorresponds(this.m, pagedb);
        StackPreserving(old(this), this);
        NonStackMemPreserving(old(this), this);
        BankedRegsInvariant(old(this), this);
        SRegsInvariant(old(this), this);
        GlobalsInvariant(old(this), this);
        r2 == old(r2);
        r3 == old(r3);
        r4 == old(r4);
        r5 == old(r5);
        r6 == old(r6);
        r7 == old(r7);
        r8 == old(r8);
        r9 == old(r9);
        r10 == old(r10);
        r11 == old(r11);
        r12 == old(r12);
        lr == old(lr);

        regs == old(seq(r1, r2, r3, r4, r5, r6, r7, r8));
        mac == svcHmacVerify(exstate, pagedb, dispPg);
        r1 == (if (regs == mac) then 1 else 0);
{
    regs := seq(r1, r2, r3, r4, r5, r6, r7, r8);

    ghost var addrspace := pagedb[dispPg].addrspace;
    assert validAddrspacePage(pagedb, addrspace) by { reveal validPageDb; }

    ghost var remaining_stack_bytes;
    remaining_stack_bytes := stack_all_regs(stack_bytes);
    lemma_SameMemAndGlobalsPreservesPageDb(old(this), this, pagedb);
    
    // Prove we still have a valid stack region after pushing registers onto the stack
    assert sp == old(sp) - WordsToBytes(14);
    calc {
        true;
        ValidMemRange(old(sp) - WordsToBytes(STACKSIZE_ATTEST + 14), old(sp));
        ValidMemRange(sp + WordsToBytes(14) - WordsToBytes(STACKSIZE_ATTEST + 14), sp + WordsToBytes(14));
        ValidMemRange(sp - WordsToBytes(STACKSIZE_ATTEST), sp + WordsToBytes(14));
            { lemma_ValidMemRange_reduced(sp - WordsToBytes(STACKSIZE_ATTEST), 14 + STACKSIZE_ATTEST, 14); }
        ValidMemRange(sp - WordsToBytes(STACKSIZE_ATTEST), sp);
    }

    ghost var mac_ptr:word;
    mac_ptr, mac := svc_returning_verify_inner(exstate, remaining_stack_bytes, pagedb, dispPg);

    remaining_stack_bytes := unstack_all_regs(remaining_stack_bytes);

    assert mac_ptr == sp - WordsToBytes(STACKSIZE_ATTEST - STACKSIZE_HMAC + 14);
    SUB(sp, sp, const(WordsToBytes(STACKSIZE_ATTEST - STACKSIZE_HMAC + 14)));

    ghost var regs_orig;
    regs_orig := compare_memory_to_regs(mac);
    assert regs == regs_orig;
    ADD(sp, sp, const(WordsToBytes(STACKSIZE_ATTEST - STACKSIZE_HMAC + 14)));
}

#verbatim
lemma lemma_BankedRegsInvariant_framing(s:state, s':state)
    requires ValidState(s) && ValidState(s')
    requires BankedRegsInvariant(s, s')
    requires mode_of_state(s) == mode_of_state(s')
    ensures s'.regs == va_update_olr(s', va_update_osp(s', va_update_reg(R12, s', va_update_reg(R11, s', va_update_reg(R10, s', va_update_reg(R9, s', va_update_reg(R8, s', va_update_reg(R7, s', va_update_reg(R6, s', va_update_reg(R5, s', va_update_reg(R4, s', va_update_reg(R3, s', va_update_reg(R2, s', va_update_reg(R1, s', va_update_reg(R0, s', s))))))))))))))).regs
{
    reveal ValidRegState();
    // sigh. help Dafny see that we're enumerating all constructors of a datatype
    assert forall r | r in s'.regs :: r.R0? || r.R1? || r.R2? || r.R3? || r.R4? || r.R5? || r.R6? || r.R7? || r.R8? || r.R9? || r.R10? || r.R11? || r.R12? || r.SP? || r.LR?;
}
#endverbatim

procedure svc_returning_verify(
    ghost exstate: state,
    ghost stack_bytes: int,
    ghost pagedb: PageDb,
    ghost dispPg: PageNr)
    reads
        globals;
    modifies
         r0; r1; r2; r3; r4; r5; r6; r7; r8; r9; r10; r11; r12; sp; lr;
         spsr_mon; mem;
    requires/ensures
        SaneState(this);
        StackBytesRemaining(this, stack_bytes);
    requires
        ValidState(exstate) && mode_of_state(exstate) != User;
        stack_bytes >= (STACKSIZE_ATTEST + 14) * WORDSIZE;  // +14 to store reigsters
        isReturningSvc(exstate);
        CoreRegPreservingExcept(exstate, this, set(OSP, OLR));
        validPageDb(pagedb);
        pageDbCorresponds(this.m, pagedb);
        finalDispatcher(pagedb, dispPg);
        GlobalFullContents(this.m, CurDispatcherOp()) == seq(page_monvaddr(dispPg));
        r0 == KOM_SVC_VERIFY_STEP2;
//        r10 == page_monvaddr(dispPg);
//        r11 == page_monvaddr(pagedb[dispPg].addrspace);
        ValidMemRange(sp - (STACKSIZE_ATTEST + 14) * WORDSIZE, sp);
        //sp - STACKSIZE_ATTEST * WORDSIZE >= KOM_DIRECTMAP_VBASE + MonitorPhysBase();
    ensures
        pageDbCorresponds(this.m, pagedb);
        StackPreserving(old(this), this);
        NonStackMemPreserving(old(this), this);
        lr == old(lr) && sp == old(sp);
        /*BankedRegsInvariant(old(this), this);
        ttbr0 == old(ttbr0) && scr == old(scr);
        this.conf.nondet == old(this.conf.nondet);
        GlobalsInvariant(old(this), this);*/
        spsr_mon == encode_mode(User);
        let tuple := svcHandled(exstate, pagedb, dispPg) in
        let retRegs := firstOf2(tuple) in
        let handled_pagedb := secondOf2(tuple) in
        pagedb == handled_pagedb && preEntryReturnRegsMatch(this, retRegs);
        preEntryReturnPreserved(old(this), this);
{
    ghost var regs := seq(r1, r2, r3, r4, r5, r6, r7, r8);

   // Giant OBSERVE to trigger CoreRegPreservingExcept
    ghost var helper_user_words := seq(OperandContents(this, OReg(R1)), 
                                       OperandContents(this, OReg(R2)), 
                                       OperandContents(this, OReg(R3)), 
                                       OperandContents(this, OReg(R4)), 
                                       OperandContents(this, OReg(R5)), 
                                       OperandContents(this, OReg(R6)), 
                                       OperandContents(this, OReg(R7)), 
                                       OperandContents(this, OReg(R8)));
    ghost var spec_user_words := seq(exstate.regs[R1], exstate.regs[R2], exstate.regs[R3], exstate.regs[R4],
                                     exstate.regs[R5], exstate.regs[R6], exstate.regs[R7], exstate.regs[R8]);
    assert regs == helper_user_words == spec_user_words;


    ghost var addrspace := pagedb[dispPg].addrspace;
    assert validAddrspacePage(pagedb, addrspace) by { reveal validPageDb; }
    ghost var dummy:word := 0;
    ghost var hmac := svcHmacVerify(exstate, pagedb, dispPg);
    ghost var ok := if regs == hmac then 1 else 0;

    ghost var regs';
    ghost var mac;
    regs', mac := svc_returning_verify_inner'(exstate, stack_bytes, pagedb, dispPg);

    //assert old(lr) == lr;
    //assert OperandContents(this, OLR) == OperandContents(old(this), OLR);
    //assert old(lr_usr) == lr_usr;
    //assert old(sp_usr) == sp_usr;

    MOV(r0, const(encode_mode(User)));
    MSR(spsr_mon, r0);

    MOV(r0, const(KOM_ERR_SUCCESS));
    MOV(r2, 0);
    MOV(r3, 0);
    MOV(r4, 0);
    MOV(r5, 0);
    MOV(r6, 0);
    MOV(r7, 0);
    MOV(r8, 0);

    // help with framing
    lemma_BankedRegsInvariant_framing(old(this), this);

    assert pageDbCorresponds(this.m, pagedb) by {
        assert globals == old(globals);
        lemma_SameMemAndGlobalsPreservesPageDb(old(this), this, pagedb);
    }

//    
//    assert mac == hmac;
//    assert r1 == ok;
//    
//    calc {
//        firstOf2(svcHandled(exstate, pagedb, dispPg));
//        tuple(KOM_ERR_SUCCESS, ok, dummy, dummy, dummy, dummy, dummy, dummy, dummy);
//        tuple(r0, r1, r2, r3, r4, r5, r6, r7, r8);
//    }
//    assert {:split_here} true;

//        var regs := (KOM_ERR_SUCCESS, ok, dummy, dummy, dummy, dummy, dummy, dummy, dummy);
//        (regs, d)
//assume false;

}
