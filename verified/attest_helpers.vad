include {:verbatim} "kom_common.i.dfy"
include {:verbatim} "pagedb.i.dfy"
include {:verbatim} "smcapi.i.dfy"
include {:verbatim} "entry.i.dfy"
include {:verbatim} "exceptions.i.dfy"

include "valedecls.vad"
include "kom_utils.vad"
include "sha/hmac.vad"

#verbatim
//const STACKSIZE_ATTEST:int := 8 + 24 + 1 + 2 * SHA_BLOCKSIZE + HMAC_KEYSIZE_WORDS + STACKSIZE_HMAC;
// workaround Dafny const-bug which results in stack overflow
const T1:int := HMAC_KEYSIZE_WORDS + STACKSIZE_HMAC;
const T2:int := 2 * SHA_BLOCKSIZE;
const T3:int := 33;
const T4:int := T1 + T2;
const STACKSIZE_ATTEST:int := T3 + T4;
#endverbatim

procedure addrspace_va_for_page_va(
    operand page_va: reg,
    out operand as_va: reg,
    out operand tmp: reg,
    ghost pagenr:PageNr,
    ghost pagedb:PageDb)
    reads
        globals;
    requires/ensures
        SaneState(this);
    requires
        @as_va != @tmp; @as_va != OSP; @tmp != OSP;
        page_va == page_monvaddr(pagenr);
        validPageDb(pagedb) && pageDbCorresponds(this.m, pagedb);
        pagedb[pagenr] is PageDbEntryTyped;
    ensures
        as_va == page_monvaddr(pagedb[pagenr].addrspace);
{
    // compute page number
    SUB(as_va, page_va, const(KOM_DIRECTMAP_VBASE));
    LDRglobaladdr(tmp, SecurePhysBaseOp());
    LDRglobal(tmp, SecurePhysBaseOp(), tmp, 0);
    SUB(as_va, as_va, tmp);
    lemma_RightShift12(as_va);
    LSR(as_va, as_va, const(PAGEBITS));
    assert as_va == monvaddr_page(old(page_va)) == pagenr;

    // retrieve addrspace field from pagedb
    // TODO: avoid pointless double-shift
    lemma_LeftShift3(as_va);
    LSL(as_va, as_va, const(PAGEDB_ENTRY_SHIFT));
    ADD(as_va, as_va, const(PAGEDB_ENTRY_ADDRSPACE));
    assert as_va == G_PAGEDB_ENTRY(pagenr) + PAGEDB_ENTRY_ADDRSPACE;
    LDRglobaladdr(tmp, PageDb());
    LDRglobal(as_va, PageDb(), tmp, as_va);

    reveal pageDbEntryCorresponds;
    PageDbCorrespondsImpliesEntryCorresponds(this.m, pagedb, pagenr);
    extractPageDbToAbstractOne(this.m, pagenr, PAGEDB_ENTRY_ADDRSPACE);
}

procedure load_attestion_key_inner() 
    reads
        globals; r4;
    modifies 
        mem; r2; r3; 
    requires/ensures
        SaneState(this);
        ValidMemWords(r4, SHA_CTXSIZE);
    ensures
        AddrMemPreservingExcept(old(mem), mem, r4, WordOffset(r4, SHA_CTXSIZE));
        //ValidMemRange(AddressOfGlobal(AttestKeyOp()), AddressOfGlobal(AttestKeyOp()) + SHA_CTXSIZE * WORDSIZE);
        AddrMemContents(mem, WordOffset(r4, 0)) == GlobalContents(globals, AttestKeyOp(), AddressOfGlobal(AttestKeyOp()) + WordsToBytes(0));
        AddrMemContents(mem, WordOffset(r4, 1)) == GlobalContents(globals, AttestKeyOp(), AddressOfGlobal(AttestKeyOp()) + WordsToBytes(1));
        AddrMemContents(mem, WordOffset(r4, 2)) == GlobalContents(globals, AttestKeyOp(), AddressOfGlobal(AttestKeyOp()) + WordsToBytes(2));
        AddrMemContents(mem, WordOffset(r4, 3)) == GlobalContents(globals, AttestKeyOp(), AddressOfGlobal(AttestKeyOp()) + WordsToBytes(3));
        AddrMemContents(mem, WordOffset(r4, 4)) == GlobalContents(globals, AttestKeyOp(), AddressOfGlobal(AttestKeyOp()) + WordsToBytes(4));
        AddrMemContents(mem, WordOffset(r4, 5)) == GlobalContents(globals, AttestKeyOp(), AddressOfGlobal(AttestKeyOp()) + WordsToBytes(5));
        AddrMemContents(mem, WordOffset(r4, 6)) == GlobalContents(globals, AttestKeyOp(), AddressOfGlobal(AttestKeyOp()) + WordsToBytes(6));
        AddrMemContents(mem, WordOffset(r4, 7)) == GlobalContents(globals, AttestKeyOp(), AddressOfGlobal(AttestKeyOp()) + WordsToBytes(7));
{
    assert ValidGlobal(AttestKeyOp()) by { reveal SaneShaGlobal; }
    LDRglobaladdr(r2, AttestKeyOp());

    LDRglobal(r3, AttestKeyOp(), r2, const(WordsToBytes(0)));
    STR(r3, r4, const(WordsToBytes(0)));
    LDRglobal(r3, AttestKeyOp(), r2, const(WordsToBytes(1)));
    STR(r3, r4, const(WordsToBytes(1)));
    LDRglobal(r3, AttestKeyOp(), r2, const(WordsToBytes(2)));
    STR(r3, r4, const(WordsToBytes(2)));
    LDRglobal(r3, AttestKeyOp(), r2, const(WordsToBytes(3)));
    STR(r3, r4, const(WordsToBytes(3)));
    LDRglobal(r3, AttestKeyOp(), r2, const(WordsToBytes(4)));
    STR(r3, r4, const(WordsToBytes(4)));
    LDRglobal(r3, AttestKeyOp(), r2, const(WordsToBytes(5)));
    STR(r3, r4, const(WordsToBytes(5)));
    LDRglobal(r3, AttestKeyOp(), r2, const(WordsToBytes(6)));
    STR(r3, r4, const(WordsToBytes(6)));
    LDRglobal(r3, AttestKeyOp(), r2, const(WordsToBytes(7)));
    STR(r3, r4, const(WordsToBytes(7)));
}

procedure mem_move_global(operand src:word, inline g:symbol, inline src_offset:word,
                          operand dst:word, inline dst_offset:word,
                          inout operand tmp:word)
    requires/ensures
        SaneState(this);
    reads 
        globals;
    modifies
        mem;
    requires
        ValidGlobalAddr(g, src + src_offset);
        ValidMem(dst + dst_offset);
        DistinctRegOperands(set(@dst, @src, @tmp), 3);
    ensures
        ValidGlobalAddr(g, src + src_offset);
        ValidMem(dst + dst_offset);
        GlobalContents(globals, g, src + src_offset) == MemContents(this.m, dst + dst_offset);
{
    LDRglobal(tmp, g, src, src_offset);
    STR(tmp, dst, dst_offset);
}

procedure load_attestion_key() returns (ghost key:seq(word))
    reads
        globals; r4;
    modifies 
        mem; r0; r2; r3; 
    requires/ensures
        SaneState(this);
        ValidMemWords(r4, SHA_CTXSIZE);
    ensures
        AddrMemPreservingExcept(old(mem), mem, r4, WordOffset(r4, SHA_CTXSIZE));
        AddrMemContentsSeq(mem, r4, SHA_CTXSIZE) == key == AttestKey();
{
    load_attestion_key_inner();

    assert WordAligned(r4);
    key := 
        seq(AddrMemContents(mem, WordOffset(r4, 0)),
            AddrMemContents(mem, WordOffset(r4, 1)),
            AddrMemContents(mem, WordOffset(r4, 2)),
            AddrMemContents(mem, WordOffset(r4, 3)),
            AddrMemContents(mem, WordOffset(r4, 4)),
            AddrMemContents(mem, WordOffset(r4, 5)),
            AddrMemContents(mem, WordOffset(r4, 6)),
            AddrMemContents(mem, WordOffset(r4, 7)));
    lemma_package_hash_result(mem, r4, AttestKey());    // Ensures AddMemContentsSeq(mem, r4, SHA_CTXSIZE) == key
    //assert key == GlobalFullContents(this.m, AttestKeyOp());
    assert GlobalFullContents(this.m, AttestKeyOp()) == AttestKey();
    //assert {:split_here} true;
}

procedure load_memory_into_regs(ghost contents:seq(word))
    reads
        mem; r0;
    modifies 
         r1; r2; r3; r4; r5; r6; r7; r8; 
    requires/ensures
        SaneState(this);
    requires
        ValidMemWords(r0, SHA_CTXSIZE);
        SeqLength(contents) == 8;
        AddrMemContentsSeq(mem, r0, SHA_CTXSIZE) == contents;
    ensures
        r1 == contents[0];
        r2 == contents[1];
        r3 == contents[2];
        r4 == contents[3];
        r5 == contents[4];
        r6 == contents[5];
        r7 == contents[6];
        r8 == contents[7];
{
    assert WordAligned(r0);
    lemma_WordAlignedAdd(r0, WordsToBytes(1));
    lemma_WordAlignedAdd(r0, WordsToBytes(2));
    lemma_WordAlignedAdd(r0, WordsToBytes(3));
    lemma_WordAlignedAdd(r0, WordsToBytes(4));
    lemma_WordAlignedAdd(r0, WordsToBytes(5));
    lemma_WordAlignedAdd(r0, WordsToBytes(6));
    lemma_WordAlignedAdd(r0, WordsToBytes(7));

    LDR(r1, r0, const(WordsToBytes(0)));
    LDR(r2, r0, const(WordsToBytes(1)));
    LDR(r3, r0, const(WordsToBytes(2)));
    LDR(r4, r0, const(WordsToBytes(3)));
    LDR(r5, r0, const(WordsToBytes(4)));
    LDR(r6, r0, const(WordsToBytes(5)));
    LDR(r7, r0, const(WordsToBytes(6)));
    LDR(r8, r0, const(WordsToBytes(7)));
}

#verbatim
lemma lemma_SeqRepeat8()
    ensures SeqRepeat(8, 0) == [0, 0, 0, 0, 0, 0, 0, 0];
{
}
#endverbatim

procedure zero_pad_memory() 
    reads
        r1;
    modifies 
        mem; r2; 
    requires/ensures
        SaneState(this);
        ValidMemWords(r1, SHA_CTXSIZE);
    ensures
        AddrMemPreservingExcept(old(mem), mem, r1, WordOffset(r1, SHA_CTXSIZE));
        AddrMemContentsSeq(mem, r1, SHA_CTXSIZE) == SeqRepeat(8, 0);
{
    MOV(r2, 0);
    STR(r2, r1, const(WordsToBytes(0)));
    STR(r2, r1, const(WordsToBytes(1)));
    STR(r2, r1, const(WordsToBytes(2)));
    STR(r2, r1, const(WordsToBytes(3)));
    STR(r2, r1, const(WordsToBytes(4)));
    STR(r2, r1, const(WordsToBytes(5)));
    STR(r2, r1, const(WordsToBytes(6)));
    STR(r2, r1, const(WordsToBytes(7)));
    
    ghost var zeroes := seq(0, 0, 0, 0, 0, 0, 0, 0);
    lemma_package_hash_result(mem, r1, zeroes);    
    lemma_SeqRepeat8();
}

#verbatim

lemma lemma_arrange_attestation_input_helper(
        user_words_mem:memmap,
        measurement_mem:memmap,
        mem:memmap,
        base:nat,
        user_words:seq<word>,
        measurement:seq<word>)
    requires ValidMemWords(base, 3 * SHA_CTXSIZE);
    requires ValidAddrMemStateOpaque(user_words_mem)
          && ValidAddrMemStateOpaque(measurement_mem)
          && ValidAddrMemStateOpaque(mem);

    requires user_words      == AddrMemContentsSeq(user_words_mem,  base,                              SHA_CTXSIZE);
    requires measurement     == AddrMemContentsSeq(measurement_mem, base + SHA_CTXSIZE * WORDSIZE,     SHA_CTXSIZE);
    requires SeqRepeat(8, 0) == AddrMemContentsSeq(mem,             base + 2 * SHA_CTXSIZE * WORDSIZE, SHA_CTXSIZE);

    requires AddrMemPreservingExcept(user_words_mem, measurement_mem, 
                                     WordOffset(base, SHA_CTXSIZE), 
                                     WordOffset(base, 2 * SHA_CTXSIZE))
    requires AddrMemPreservingExcept(measurement_mem, mem,
                                     WordOffset(base, 2 * SHA_CTXSIZE), 
                                     WordOffset(base, 3 * SHA_CTXSIZE))
    ensures AddrMemContentsSeq(mem, base, 3 * SHA_CTXSIZE) == user_words + measurement + SeqRepeat(8, 0);
{
    calc {
        true;
        ValidMemRange(base, base + 3 * SHA_CTXSIZE * WORDSIZE);
            { lemma_ValidMemRange_reduced(base, 3 * SHA_CTXSIZE, 2 * SHA_CTXSIZE); }
        ValidMemRange(base, base + SHA_CTXSIZE * WORDSIZE);
    }
    
    calc {
        true;
        ValidMemRange(base, base + 3 * SHA_CTXSIZE * WORDSIZE);
            { lemma_ValidMemRange_offset(base, 3 * SHA_CTXSIZE, SHA_CTXSIZE); }
        ValidMemRange(base + SHA_CTXSIZE * WORDSIZE, base + 2 * SHA_CTXSIZE * WORDSIZE);
    }
    
    calc {
        true;
        ValidMemRange(base, base + 3 * SHA_CTXSIZE * WORDSIZE);
            { lemma_ValidMemRange_offset(base, 3 * SHA_CTXSIZE, 2 * SHA_CTXSIZE); }
        ValidMemRange(base + 2 * SHA_CTXSIZE * WORDSIZE, base + 3 * SHA_CTXSIZE * WORDSIZE);
    }

    // Propagate memory contents to latest version of memory
    calc {
        user_words;
        AddrMemContentsSeq(user_words_mem, base, SHA_CTXSIZE);
            { lemma_AddrMemContentsSeq_framing1(user_words_mem, measurement_mem, 
                                     base, SHA_CTXSIZE, 
                                     base +     SHA_CTXSIZE * WORDSIZE, 
                                     base + 2 * SHA_CTXSIZE * WORDSIZE); }
        AddrMemContentsSeq(measurement_mem, base, SHA_CTXSIZE);
            { lemma_AddrMemContentsSeq_framing1(measurement_mem, mem,
                                     base, SHA_CTXSIZE, 
                                     base + 2 * SHA_CTXSIZE * WORDSIZE, 
                                     base + 3 * SHA_CTXSIZE * WORDSIZE); }
        AddrMemContentsSeq(mem, base, SHA_CTXSIZE);
    }

    calc {
        measurement;
        AddrMemContentsSeq(measurement_mem, base + SHA_CTXSIZE * WORDSIZE, SHA_CTXSIZE);
            { lemma_AddrMemContentsSeq_framing1(measurement_mem, mem, 
                                                base + SHA_CTXSIZE * WORDSIZE, SHA_CTXSIZE,
                                                base + 2 * SHA_CTXSIZE * WORDSIZE, 
                                                base + 3 * SHA_CTXSIZE * WORDSIZE);
            }
        AddrMemContentsSeq(mem, base + SHA_CTXSIZE * WORDSIZE, SHA_CTXSIZE);
    }


    calc {
        AddrMemContentsSeq(mem, base, 3 * SHA_CTXSIZE);
            { lemma_AddrMemContentsSeq_adds(mem, base, 3 * SHA_CTXSIZE, SHA_CTXSIZE); }
        AddrMemContentsSeq(mem, base, SHA_CTXSIZE) 
      + AddrMemContentsSeq(mem, base + SHA_CTXSIZE * WORDSIZE, 2 * SHA_CTXSIZE);
        user_words + AddrMemContentsSeq(mem, base + SHA_CTXSIZE * WORDSIZE, 2 * SHA_CTXSIZE);
            { lemma_AddrMemContentsSeq_adds(mem, base + SHA_CTXSIZE * WORDSIZE, 2 * SHA_CTXSIZE, SHA_CTXSIZE); }
        user_words 
      + AddrMemContentsSeq(mem, base + SHA_CTXSIZE * WORDSIZE, SHA_CTXSIZE) 
      + AddrMemContentsSeq(mem, base + SHA_CTXSIZE * WORDSIZE + SHA_CTXSIZE * WORDSIZE, SHA_CTXSIZE);

        user_words 
      + AddrMemContentsSeq(mem, base + SHA_CTXSIZE * WORDSIZE, SHA_CTXSIZE) 
      + AddrMemContentsSeq(mem, base + 2 * SHA_CTXSIZE * WORDSIZE, SHA_CTXSIZE);

        user_words 
      + measurement
      + AddrMemContentsSeq(mem, base + 2 * SHA_CTXSIZE * WORDSIZE, SHA_CTXSIZE);

        user_words 
      + measurement
      + SeqRepeat(8, 0);
    }
}
#endverbatim


procedure stack_all_regs(ghost stack_bytes:int)
    returns (ghost stack_bytes_ret:int)
    reads
        r0; r1; r2; r3; r4; r5; r6; r7; r8; r9; r10; r11; r12; lr;
    modifies
        sp; mem;
    requires/ensures
        SaneState(this);
    requires
        stack_bytes >= WordsToBytes(14);
        StackBytesRemaining(this,stack_bytes);
    ensures
        sp == old(sp-WordsToBytes(14));
 
        MemContents(this.m, sp)    == old(r12);
        MemContents(this.m, WordOffset(sp, 1))  == old(r11);
        MemContents(this.m, WordOffset(sp, 2))  == old(r10);
        MemContents(this.m, WordOffset(sp, 3)) == old(r9);
        MemContents(this.m, WordOffset(sp, 4)) == old(r8);
        MemContents(this.m, WordOffset(sp, 5)) == old(r7);
        MemContents(this.m, WordOffset(sp, 6)) == old(r6);
        MemContents(this.m, WordOffset(sp, 7)) == old(r5);
        MemContents(this.m, WordOffset(sp, 8)) == old(r4);
        MemContents(this.m, WordOffset(sp, 9)) == old(r3);
        MemContents(this.m, WordOffset(sp, 10)) == old(r2);
        MemContents(this.m, WordOffset(sp, 11)) == old(r1);
        MemContents(this.m, WordOffset(sp, 12)) == old(r0);
        MemContents(this.m, WordOffset(sp, 13)) == old(lr);

        NonStackMemPreserving(old(this),this);
        ParentStackPreserving(old(this),this);

        stack_bytes_ret == stack_bytes-WordsToBytes(14);
        StackBytesRemaining(this,stack_bytes_ret);
{
    SUB(sp, sp, const(WordsToBytes(14)));

    STR(lr,  sp, const(WordsToBytes(13)));
    STR(r0,  sp, const(WordsToBytes(12)));
    STR(r1,  sp, const(WordsToBytes(11)));
    STR(r2,  sp, const(WordsToBytes(10)));
    STR(r3,  sp, const(WordsToBytes(9)));
    STR(r4,  sp, const(WordsToBytes(8)));
    STR(r5,  sp, const(WordsToBytes(7)));
    STR(r6,  sp, const(WordsToBytes(6)));
    STR(r7,  sp, const(WordsToBytes(5)));
    STR(r8,  sp, const(WordsToBytes(4)));
    STR(r9,  sp, const(WordsToBytes(3)));
    STR(r10, sp, const(WordsToBytes(2)));
    STR(r11, sp, const(WordsToBytes(1)));
    STR(r12, sp, 0);

    stack_bytes_ret := stack_bytes - WordsToBytes(14);
}

procedure unstack_all_regs(ghost stack_bytes:int)
    returns (ghost stack_bytes_ret:int)
    reads
        mem;
    modifies
        r0; r1; r2; r3; r4; r5; r6; r7; r8; r9; r10; r11; r12; lr; sp;
    requires/ensures
        SaneState(this);
    requires
        isUInt32(sp + WordsToBytes(14));
        sp + WordsToBytes(14) <= StackBase();
        StackBytesRemaining(this, stack_bytes);
    ensures
        sp == old(sp+WordsToBytes(14));
        
        old(MemContents(this.m, sp))    == r12;
        old(MemContents(this.m, WordOffset(sp, 1)))  == r11;
        old(MemContents(this.m, WordOffset(sp, 2)))  == r10;
        old(MemContents(this.m, WordOffset(sp, 3))) == r9;
        old(MemContents(this.m, WordOffset(sp, 4))) == r8;
        old(MemContents(this.m, WordOffset(sp, 5))) == r7;
        old(MemContents(this.m, WordOffset(sp, 6))) == r6;
        old(MemContents(this.m, WordOffset(sp, 7))) == r5;
        old(MemContents(this.m, WordOffset(sp, 8))) == r4;
        old(MemContents(this.m, WordOffset(sp, 9))) == r3;
        old(MemContents(this.m, WordOffset(sp, 10))) == r2;
        old(MemContents(this.m, WordOffset(sp, 11))) == r1;
        old(MemContents(this.m, WordOffset(sp, 12))) == r0;
        old(MemContents(this.m, WordOffset(sp, 13))) == lr;

        stack_bytes_ret == stack_bytes + WordsToBytes(14);
        StackBytesRemaining(this, stack_bytes_ret);
{
    LDR(r12, sp, 0);
    LDR(r11, sp, const(WordsToBytes(1)));
    LDR(r10, sp, const(WordsToBytes(2)));
    LDR(r9,  sp, const(WordsToBytes(3)));
    LDR(r8,  sp, const(WordsToBytes(4)));
    LDR(r7,  sp, const(WordsToBytes(5)));
    LDR(r6,  sp, const(WordsToBytes(6)));
    LDR(r5,  sp, const(WordsToBytes(7)));
    LDR(r4,  sp, const(WordsToBytes(8)));
    LDR(r3,  sp, const(WordsToBytes(9)));
    LDR(r2,  sp, const(WordsToBytes(10)));
    LDR(r1,  sp, const(WordsToBytes(11)));
    LDR(r0,  sp, const(WordsToBytes(12)));
    LDR(lr,  sp, const(WordsToBytes(13)));

    ADD(sp, sp, const(WordsToBytes(14)));
    stack_bytes_ret := stack_bytes + WordsToBytes(14);
}
